plugins {
    id 'net.minecrell.licenser' version '0.4.1'
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    //id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'com.gradle.plugin-publish' version "0.10.1"
    id 'java-gradle-plugin'
    id 'com.jfrog.artifactory' version '4.15.1'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

group = 'net.minecraftforge.gradle'
version = gitVersion()

boolean snapshot = true

def _BINTRAY_USER = System.getenv('BINTRAY_USER')
def _BINTRAY_KEY = System.getenv('BINTRAY_KEY')
def _REPO_KEY = snapshot ? 'oss-snapshot-local' : 'oss-release-local'

def gitVersion() {
    def raw = grgit.describe(longDescr: true, tags: true)
    def desc = (raw == null ? 'unknown-unknown-unknown' : raw).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name
    return "${tag}.${offset}" //${t -> if (branch != 'master') t << '-' + branch}"
}
println('Version: ' + version + ' Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets {
    common
    mcp
    patcher
    userdev
}

configurations {
    all {
        exclude group: 'org.ow2.asm'
    }
    mcpImplementation.extendsFrom commonImplementation
    patcherImplementation.extendsFrom commonImplementation
    userdevImplementation.extendsFrom mcpImplementation
    implementation.extendsFrom mcpImplementation, patcherImplementation, userdevImplementation
}

jar {
    from sourceSets.common.output
    from sourceSets.mcp.output
    from sourceSets.patcher.output
    from sourceSets.userdev.output
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/public/' }
    maven { url 'https://lss233.littleservice.cn/repositories/minecraft' }
    maven { url 'https://files.minecraftforge.net/maven' }
}

license {
    header = file('HEADER')
    ext {
        name = 'ForgeGradle'
        year = 2018
        fullname = 'Forge Development LLC'
    }
    exclude '**/*.properties'
    exclude 'net/minecraftforge/gradle/common/diff/'
    exclude 'net/minecraftforge/gradle/common/util/JavaVersionParser.java'
}

dependencies {
    commonImplementation gradleApi()
    commonImplementation 'com.cloudbees:diff4j:1.2'
    commonImplementation 'commons-io:commons-io:2.4'
    commonImplementation 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    commonImplementation 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    commonImplementation 'com.google.code.gson:gson:2.8.5'
    commonImplementation 'com.google.guava:guava:26.0-jre'
    commonImplementation 'de.siegmar:fastcsv:1.0.2'
    commonImplementation 'net.minecraftforge:artifactural:1.0.+'
    commonImplementation 'org.apache.maven:maven-artifact:3.6.0'
    commonImplementation 'org.apache.httpcomponents:httpclient:4.3.3'
    commonImplementation 'net.minecraftforge:srgutils:0.2.12'

    mcpImplementation sourceSets.common.output
    patcherImplementation sourceSets.mcp.output
    patcherImplementation sourceSets.common.output
    userdevImplementation sourceSets.mcp.output
    userdevImplementation sourceSets.common.output
}

//Gradle doesn't add it's own source when doing the API. So lets hack it in!

import org.gradle.plugins.ide.eclipse.model.AbstractLibrary
import org.gradle.plugins.ide.eclipse.model.Classpath
import org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory

project.extensions.eclipse.classpath.file.whenMerged { Classpath cp ->
    def gradleSrc = gradle.gradleHomeDir.absolutePath.replace(File.separator, '/') + '/src/'
    cp.entries.each { entry ->
        if ((entry in AbstractLibrary) && entry.library.file.name.startsWith('gradle-')) {
            def type = (entry.library.file.name =~ "^gradle(-(.*))?-(${gradle.gradleVersion})")[0][2]
            if (type == 'api') type = 'core-api' //Gradle name is different for cores
            if (type == '') type = 'core'
            entry.sourcePath = new FileReferenceFactory().fromPath(gradleSrc + type)
        }
    }
}

gradlePlugin {
    plugins {
        mcpPlugin{
            id = 'org.soraworld.forge-mcp'
            implementationClass = 'net.minecraftforge.gradle.mcp.MCPPlugin'
        }
        patcherPlugin{
            id = 'org.soraworld.forge-patcher'
            implementationClass = 'net.minecraftforge.gradle.patcher.PatcherPlugin'
        }
        userDevPlugin {
            id = 'org.soraworld.forge-userdev'
            implementationClass = 'net.minecraftforge.gradle.userdev.UserDevPlugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'org.soraworld'
            artifactId = 'forge-gradle-uri'
            version = project.version + (snapshot ? '-SNAPSHOT' : '')
        }
    }
}

artifactory {
    setContextUrl('https://oss.jfrog.org/artifactory')
    publish {
        repository {
            repoKey = _REPO_KEY
            username = _BINTRAY_USER
            password = _BINTRAY_KEY
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}
